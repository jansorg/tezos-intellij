{
    generate=[names="long" visitor-value="R"]

    // if this is a composite element then it triggers a different factory in PowershellElementTypes using IElementType
    implements="com.tezos.lang.michelson.psi.MichelsonComposite"
    extends="com.tezos.lang.michelson.psi.impl.MichelsonCompositeImpl"

    parserClass="com.tezos.lang.michelson.parser.MichelsonParser"
    parserUtilClass="com.tezos.lang.michelson.parser.MichelsonParserUtil"

    psiClassPrefix="Psi"
    psiImplClassSuffix="Impl"
    psiPackage="com.tezos.lang.michelson.parser"
    psiImplPackage="com.tezos.lang.michelson.parser.impl"

    tokenTypeClass="com.tezos.lang.michelson.lexer.MichelsonElementType"
    elementTypeClass="com.tezos.lang.michelson.psi.MichelsonCompositeElementType"
    elementTypeHolderClass="com.tezos.lang.michelson.MichelsonTypes"

    extends(".*_type")=type
    extends(".*_data")=data
    extends(".*_section")=section

    tokens=[
        LEFT_PAREN='('
        RIGHT_PAREN=')'
        LEFT_CURLY='{'
        RIGHT_CURLY='}'
        SEMI=';'

        INT='regexp:-?[0-9]+'
        STRING='regexp:"[^"]*"' //fixme handle escape codes?
        BYTE='regexp:0x[A-F0-9]+' //fixme: add lowercase characters?
        TAG='regexp:[A-Z][a-z]+'
        NAME='regexp:[a-z]+'
        // from https://gitlab.com/tezos/tezos/blob/master/emacs/michelson-mode.el
        INSTRUCTION_TOKEN='regexp:[A-Z][A-Z_0-9]*'

//        Unit='Unit'
//        TRUE='True'
//        FALSE='False'
//        TIMESTAMP_STRING='regexp:"\d{4}-\d{2}-\d{2}T\d{2}:\d{2}:\d{2}\w+"'
//        SIGNATURE_STRING=''
//        KEY_STRING=''
//        KEY_HASH_STRING=''
//        TEZ_STRING=''
//        CONTRACT_STRING=''

        //fixme add comment tokens
        COMMENT_LINE='regexp:#.*'
        COMMENT_MULTI_LINE='regexp:/\* ~\*/'
    ]
}

private script_file ::= section*

section ::= parameter_section | return_section | storage_section | code_section
parameter_section ::= 'parameter' toplevel_type ';'
return_section ::= 'return' toplevel_type ';'
storage_section ::= 'storage' toplevel_type ';'
code_section ::= 'code' instruction ';'

private toplevel_data ::=
    '(' data ')'
  | INT
  | STRING
//  | TIMESTAMP_STRING
//  | SIGNATURE_STRING
//  | KEY_STRING
//  | KEY_HASH_STRING
//  | TEZ_STRING
//  | CONTRACT_STRING
  | 'Unit'
  | 'True'
  | 'False'

data ::=
    toplevel_data
  | 'Pair' toplevel_data toplevel_data
  | 'Left' toplevel_data
  | 'Right' toplevel_data
  | 'Some' toplevel_data
  | 'None'
  | '{' (toplevel_data (';' toplevel_data)*)? '}' //fixme
  | '{' ('Elt' toplevel_data toplevel_data (';' 'Elt' toplevel_data toplevel_data)*)? '}' //fixme
  | instruction //fixme

instructions ::= '{' (instruction (';' instruction)*)? '}'

instruction ::=
    instructions
  | 'DROP'
  | 'DUP'
  | 'SWAP'
  | 'PUSH' toplevel_type toplevel_data
  | 'SOME'
  | 'NONE' toplevel_type
  | 'UNIT'
  | 'IF_NONE' instructions instructions
  | 'PAIR'
  | 'CAR'
  | 'CDR'
  | 'LEFT' toplevel_type
  | 'RIGHT' toplevel_type
  | 'IF_LEFT' instructions instructions
  | 'NIL' toplevel_type
  | 'CONS'
  | 'IF_CONS' instructions instructions
  | 'EMPTY_SET' type
  | 'EMPTY_MAP' comparable_type type
  | 'MAP' instructions
  | 'ITER' instructions
  | 'MEM'
  | 'GET'
  | 'UPDATE'
  | 'IF' instructions instructions
  | 'LOOP' instructions
  | 'LOOP_LEFT' instructions
  | 'LAMBDA' type type instructions
  | 'EXEC'
  | 'DIP' instructions
  | 'FAILWITH'
  | 'CAST'
  | 'RENAME'
  | 'CONCAT'
  | 'ADD'
  | 'SUB'
  | 'MUL'
  | 'DIV'
  | 'ABS'
  | 'NEG'
  | 'MOD'
  | 'LSL'
  | 'LSR'
  | 'OR'
  | 'AND'
  | 'XOR'
  | 'NOT'
  | 'COMPARE'
  | 'EQ'
  | 'NEQ'
  | 'LT'
  | 'GT'
  | 'LE'
  | 'GE'
  | 'INT'
  | 'SELF'
  | 'TRANSFER_TOKENS'
  | 'SET_DELEGATE'
  | 'CREATE_ACCOUNT'
  | 'CREATE_CONTRACT'
  | 'IMPLICIT_ACCOUNT'
  | 'NOW'
  | 'AMOUNT'
  | 'BALANCE'
  | 'CHECK_SIGNATURE'
  | 'BLAKE2B'
  | 'HASH_KEY'
  | 'STEPS_TO_QUOTA'
  | 'SOURCE'
  | 'SENDER'

private toplevel_type ::=
    '(' type ')'
  | 'key'
  | 'unit'
  | 'signature'
  | 'operation'
  | comparable_type

type ::=
    toplevel_type
  | 'option' type
  | 'list' type
  | 'set' comparable_type
  | 'contract' type
  | 'pair' type type
  | 'or' type type
  | 'lambda' type type
  | 'map' comparable_type type
  | 'big_map' comparable_type type

comparable_type ::=
    'int'
  | 'nat'
  | 'string'
  | 'tez'
  | 'bool'
  | 'key_hash'
  | 'timestamp'
